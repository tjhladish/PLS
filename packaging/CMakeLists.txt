cmake_minimum_required(VERSION 3.16)

set(pls_known_comps static shared)
set(pls_comp_static NO)
set(pls_comp_shared NO)
foreach (pls_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (pls_comp IN_LIST pls_known_comps)
        set(pls_comp_${pls_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "PLS does not recognize component `${pls_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (pls_comp_static AND pls_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "PLS `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(pls_static_targets "${CMAKE_CURRENT_LIST_DIR}/PLS-static-targets.cmake")
set(pls_shared_targets "${CMAKE_CURRENT_LIST_DIR}/PLS-shared-targets.cmake")

macro(pls_load_targets type)
    if (NOT EXISTS "${pls_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "PLS `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${pls_${type}_targets}")
endmacro()

if (pls_comp_static)
    pls_load_targets(static)
elseif (pls_comp_shared)
    pls_load_targets(shared)
elseif (DEFINED pls_SHARED_LIBS AND pls_SHARED_LIBS)
    pls_load_targets(shared)
elseif (DEFINED pls_SHARED_LIBS AND NOT pls_SHARED_LIBS)
    pls_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${pls_shared_targets}")
        pls_load_targets(shared)
    else ()
        pls_load_targets(static)
    endif ()
else ()
    if (EXISTS "${pls_static_targets}")
        pls_load_targets(static)
    else ()
        pls_load_targets(shared)
    endif ()
endif ()